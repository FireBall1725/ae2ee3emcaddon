apply plugin: 'forge'
apply plugin: 'curseforge'

apply from: 'gradle/scripts/dependencies.gradle'
apply from: 'gradle/scripts/artifacts.gradle'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 7200, 'hours'
}

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

ext {
    curseApiKey = ""
    curseReleaseType = ""
}

version = minecraft_version + "-" + "b" + cibuild + "-universal"
archivesBaseName = cibasename

// If TeamCity is running this build, lets set the version info
if (hasProperty("teamcity")) {
    version = minecraft_version + "-" + teamcity["build.number"] + "-universal"

    // Fix for main branch being built
    version = version.replaceAll("/", "-")

    curseApiKey = teamcity["apiKey"]
    curseReleaseType = teamcity["releaseType"]
}

minecraft {
    version = minecraft_version + "-" + forge_version

    replace "@version@", project.version
    replace "@MCVERSION@", minecraft_version

    // used when launching minecraft in dev env
    runDir = "run"
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/java/'
        }

        resources {
            srcDir "src/main/resources/"
            include "mcmod.info"
        }
    }
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': minecraft_version
    }
}

curse {
    apiKey = curseApiKey
    releaseType = curseReleaseType
    projectId = "230951"
    changelog = file("build/libs/changelog.txt").text
    addGameVersion minecraft_version
}